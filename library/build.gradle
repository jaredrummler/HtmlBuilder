apply plugin: 'com.android.library'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 25
  }
}

dependencies {
  compile 'com.android.support:support-annotations:25.1.0'
  testCompile 'junit:junit:4.12'
}

apply from: 'https://raw.githubusercontent.com/jaredrummler/android-artifact-push/master/maven/gradle-mvn-push.gradle'

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name == com.android.builder.core.BuilderConstants.DEBUG) return // Skip debug builds.
  def task = project.tasks.create "jar${name.capitalize()}", Jar
  task.group = "Publications"
  task.description = "Create compiled jar of project."
  task.classifier = "binary"
  task.baseName = project.name + '-' + name
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  artifacts.add('archives', task)
}
